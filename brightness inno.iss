; Script generated by the Inno Script Studio Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "BrightKeyMap"
#define MyAppVersion "1.0"
#define MyAppPublisher "Wade's Tech Trove"
#define MyAppURL "https://wadestechtrove.blogspot.com/"
#define MyAppExeName "MapBrightnessKeys.exe"
#define MyUninstallerName "Uninstall"

[Setup]
PrivilegesRequired=admin
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{FA00390E-A597-4EA6-8EDF-73054BA2DCBA}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
InfoBeforeFile=READMEPRE.rtf
InfoAfterFile=READMEEND.rtf
OutputBaseFilename=BrightKeyMap Setup
;SetupIconFile=brightness.ico
Compression=lzma
SolidCompression=yes
AlwaysRestart=False
ArchitecturesInstallIn64BitMode=x64
UninstallFilesDir={app}\Uninstall

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Dirs]
Name: "{app}\Uninstall"; Attribs: hidden

[Files]
Source: "AHK_Compiler\Ahk2Exe.exe"; DestDir: "{app}\AHK_Compiler";
Source: "AHK_Compiler\ANSI 32-bit.bin"; DestDir: "{app}\AHK_Compiler";
Source: "AHK_Compiler\AutoHotkeySC.bin"; DestDir: "{app}\AHK_Compiler";
Source: "AHK_Compiler\Unicode 32-bit.bin"; DestDir: "{app}\AHK_Compiler";
Source: "AHK_Compiler\Unicode 64-bit.bin"; DestDir: "{app}\AHK_Compiler";
Source: "blank_MappedKeys.ahk"; DestDir: "{app}"; Flags: ignoreversion
Source: "brightness.ico"; DestDir: "{app}";
Source: "MapBrightnessKeys.ps1"; DestDir: "{tmp}";

[Icons]
Name: "{app}\{#MyUninstallerName}"; Filename: "{app}\Uninstall\unins000.exe"
;Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"

[Registry]
Root: HKLM; Subkey: "SOFTWARE\Microsoft\Windows\CurrentVersion\Policies\System"; ValueType: dword; ValueName: "DisableAutomaticRestartSignOn"; ValueData: "00000001"

[Run]
Filename: "powershell.exe"; \
  Parameters: "-NoProfile -ExecutionPolicy Bypass -File ""{tmp}\MapBrightnessKeys.ps1"""; \
  WorkingDir: {app}; Flags: runascurrentuser

[UninstallRun]
Filename: "schtasks"; \
    Parameters: "/DELETE /F /TN ""BrightKeyMap""";

[UninstallDelete]
Type: files; Name: "{app}\BrightKeyMap.exe"
Type: dirifempty; Name: "{app}"

[Code]
function InitializeUninstall(): Boolean;
  var ErrorCode: Integer;
begin
  ShellExec('open','taskkill.exe','/f /im BrightKeyMap.exe','',SW_HIDE,ewNoWait,ErrorCode);
  ShellExec('open','tskill.exe',' BrightKeyMap.exe','',SW_HIDE,ewNoWait,ErrorCode);
  result := True;
end;
